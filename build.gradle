buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.0.9'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.9'
    id 'net.franz-becker.gradle-lombok' version '1.5'
}

def api = new groovy.json.JsonSlurper().parseText(file("${api_token_path}").text)
def mcVersion = project.mc_version

apply plugin: 'net.minecraftforge.gradle.forge'
version = project.version
group = "stevekung.mods.moreplanets"
archivesBaseName = "More-Planets-All"
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "1.11.2-13.20.1.2563"
    runDir = "run"
    mappings = "snapshot_20161220"
    makeObfSourceJar = false
}

repositories {
    // Hwyla
    maven {
        url "http://tehnut.info/maven/"
    }
    // JEI
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    // Code Chicken mods
    maven {
        url "http://chickenbones.net/maven/"
    }
    // Curse Gradle
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    // CTM
    maven {
        url "http://maven.tterrag.com/"
    }
}

dependencies {
    compile "mezz.jei:jei_${mc_version}:${jei_version}:api"
    compile "codechicken:NotEnoughItems:${mc_version}-${nei_version}:deobf"
    compile "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:deobf"
    deobfCompile "team.chisel.ctm:CTM:MC${mc_version}-${ctm_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${hwyla_version}"
    runtime "mezz.jei:jei_${mc_version}:${jei_version}"
    runtime "codechicken:NotEnoughItems:${mc_version}-${nei_version}:deobf"
    runtime "codechicken:CodeChickenLib:${mc_version}-${ccl_version}:deobf"
}

curseforge {
    apiKey = api.more_planets
    project {
        id = '261990'
        changelog = 'To read full change log use command /mpchangelog in-game.\n\n or following by this link: https://discord.gg/5KDqYh2'
        releaseType = 'release'
        addGameVersion mcVersion

        mainArtifact(file('build/libs/More-Planets-' + mcVersion + '-' + version + '.jar')) {
            displayName = "More-Planets-" + mcVersion + "-" + version
        }
    }
}

lombok {
    version = "1.16.6"
    sha256 = "e0a471be03e1e6b02bf019480cec7a3ac9801702bf7bf62f15d077ad4df8dd5d"
}

ext.coreJar = "build/libs/" + archivesBaseName + "-" + version + ".jar"
ext.coreFiles = ['stevekung/mods/moreplanets/**', 'assets/moreplanets/**', 'META-INF/moreplanets_at.cfg']

// build normal jar
task buildJar(type: Jar, dependsOn: "rePackAllJar") {
    outputs.upToDateWhen { false }
    baseName = "More-Planets-" + mcVersion

    from(zipTree(project.ext.coreJar)) {
        includes.addAll(project.ext.coreFiles)
    }
    manifest {
        attributes.put("FMLAT", "moreplanets_at.cfg")
        attributes.put("FMLCorePluginContainsFMLMod", "true")
    }
}

task rePackAllJar(type: Jar, dependsOn: "copyModFiles") {
    from 'temporary'
}

task copyModFiles(type: Copy, dependsOn: "reobfJar") {
    from(zipTree(project.ext.coreJar))
    into 'temporary'
}

// build deobf jar
task buildDeobfJar(type: Jar, dependsOn: "rePackAllDeobfJar") {
    outputs.upToDateWhen { false }
    baseName = "More-Planets-" + mcVersion
    classifier = 'deobf'

    from(zipTree(project.ext.coreJar)) {
        includes.addAll(project.ext.coreFiles)
    }
    manifest {
        attributes.put("FMLAT", "moreplanets_at.cfg")
        attributes.put("FMLCorePluginContainsFMLMod", "true")
    }
}

task rePackAllDeobfJar(type: Jar, dependsOn: "copyDeobfModFiles") {
    from 'temporary_deobf'
}

task copyDeobfModFiles(type: Copy, dependsOn: "reobfJar") {
    from sourceSets.main.output
    into 'temporary_deobf'
}

// delete temp folders
task deleteTemporary(type: Delete) {
    delete 'temporary'
    delete 'temporary_deobf'
}

buildJar.finalizedBy(deleteTemporary)
buildDeobfJar.finalizedBy(deleteTemporary)